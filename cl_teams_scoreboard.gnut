global function Teams_RegisterSignals
global function Teams_AddCallback_ScoreboardData
global function Teams_AddCallback_PlayerScores
global function Teams_AddCallback_SortScoreboardPlayers
global function Teams_AddCallback_Header
global function Teams_AddCallback_ScoreboardTitle
global function Teams_AddCallback_GetTeamColor
global function Teams_AddCallback_OnPlayerClick
global function Teams_AddCallback_IsEnabled
global function Teams_PopulatePlayerRow
global function Teams_PopulateHeaderRui
global function Teams_GetMaxTeamSize
global function Teams_GetTeamColor

global function UICallback_ScoreboardMenu_BindTeamHeader
global function UICallback_ScoreboardMenu_BindTeamRow
global function UICallback_Scoreboard_OnReportClicked

global function ClientCallback_Teams_SetScoreboardData
global function ClientCallback_Teams_SetScoreboardTitle
global function ClientCallback_Teams_CloseScoreboard
global function ClientCallback_Teams_OnPlayerClicked

global function IsLocalPlayerOnTeamSpectatorWithCallback

global struct ScoreboardData
{
	int             numScoreColumns
	array< int > 	columnNumDigits
	array< asset >  columnDisplayIcons
	array< float >  columnDisplayIconsScale
}

                                   
global struct TeamsScoreboardPlayerStats
{
	string score
	int numDigits
}

global struct TeamsScoreboardPlayer
{
	EHI playerEHI                           
	entity player

	string playerName
	string platformIcon

	asset playerImage
	asset playerBG

	bool isRatingLeader
	bool isKillLeader
	bool isFriendly
	bool isLocalPlayer

	int respawnStatus
	bool isDisconnected
	int bleedoutEndTime

	bool isValid = false

	vector customCharacterColor
	bool useCustomCharacterColor

	vector enemyBackgroundColor

	array< TeamsScoreboardPlayerStats > stats
	int team
}

struct
{
	table< int, array< TeamsScoreboardPlayer > > teamsOfPlayers
} teamsScoreboardCachedData

struct {
	ScoreboardData functionref( ) Callback_ScoreboardData
	array< string > functionref( entity ) Callback_PlayerScores
	array< entity > functionref( array< entity >, ScoreboardData ) Callback_SortScoreboardPlayers
	void functionref( var, var, int ) Callback_Header
	vector functionref( int ) Callback_GetTeamColor
	string functionref() Callback_ScoreboardTitle
	vector functionref( EHI ) Callback_OnPlayerClick
	bool functionref() Callback_IsEnabled

	CustomMatch_LobbyState CustomMatchData
} file

void function Teams_RegisterSignals()
{
	RegisterSignal( "Teams_HideScoreboard" )
	RegisterSignal( "Teams_OnResolutionChanged" )

	AddClientCallback_OnResolutionChanged( Teams_OnResolutionChanged )

	AddCallback_GameStateEnter( eGameState.Resolution, Teams_OnResolution )
}

void function Teams_OnResolutionChanged()
{
	Signal( clGlobal.levelEnt, "Teams_OnResolutionChanged" )
}

           
void function Teams_AddCallback_ScoreboardTitle( string functionref() callbackFunc )
{
	Assert( !IsValid( file.Callback_ScoreboardTitle ), "ScoreboardTitle already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_ScoreboardTitle = callbackFunc
}

void function Teams_AddCallback_ScoreboardData( ScoreboardData functionref(  ) callbackFunc )
{
	Assert( !IsValid( file.Callback_ScoreboardData ), "ScoreboardData already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_ScoreboardData = callbackFunc
}

void function Teams_AddCallback_PlayerScores( array< string > functionref( entity ) callbackFunc )
{
	Assert( !IsValid( file.Callback_PlayerScores ), "PlayerScores already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_PlayerScores = callbackFunc
}

void function Teams_AddCallback_SortScoreboardPlayers(  array< entity > functionref( array< entity >, ScoreboardData ) callbackFunc )
{
	Assert( !IsValid( file.Callback_SortScoreboardPlayers ), "SortScoreboardPlayers already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_SortScoreboardPlayers = callbackFunc
}

void function Teams_AddCallback_Header(  void functionref( var, var, int ) callbackFunc )
{
	Assert( !IsValid( file.Callback_Header ), "_Header already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_Header = callbackFunc
}

void function Teams_AddCallback_GetTeamColor(  vector functionref( int ) callbackFunc )
{
	Assert( !IsValid( file.Callback_GetTeamColor ), "_GetTeamColor already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_GetTeamColor = callbackFunc
}

void function Teams_AddCallback_OnPlayerClick(  vector functionref( EHI ) callbackFunc )
{
	Assert( !IsValid( file.Callback_OnPlayerClick ), "_OnPlayerClick already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_OnPlayerClick = callbackFunc
}

void function Teams_AddCallback_IsEnabled(  bool functionref() callbackFunc )
{
	Assert( !IsValid( file.Callback_IsEnabled ), "_IsEnabled already has a callback function - " + string( callbackFunc ) + ", ensure this is only called once" )
	file.Callback_IsEnabled = callbackFunc
}

void function IsLocalPlayerOnTeamSpectatorWithCallback( var button )
{
	RunUIScript( "SetPlayerTooltipAfterCallback", button, IsLocalPlayerOnTeamSpectator() )
}

         
ScoreboardData function GetScoreboardData()
{
	if( IsValid(file.Callback_ScoreboardData) && !IsObserverInStartOfPrivateGame() )
	{
		return file.Callback_ScoreboardData()
	}
	else
	{
		ScoreboardData data
		data.numScoreColumns = 0

		return data
	}

	unreachable
}

array<string> function GetPlayerScores( entity player )
{
	if( IsValid(file.Callback_PlayerScores) && !IsObserverInStartOfPrivateGame() )
	{
		return file.Callback_PlayerScores( player )
	}
	else
	{
		array<string> empty = []

		return empty
	}

	unreachable
}

bool function IsObserverInStartOfPrivateGame()
{
	return IsPrivateMatch() && GetLocalClientPlayer().IsObserver() && GetLocalClientPlayer().GetTeam() == TEAM_SPECTATOR && GetGameState() < eGameState.Playing
}

bool function IsObserverInPrivateGame()
{
	return IsPrivateMatch() && GetLocalClientPlayer().IsObserver() && GetLocalClientPlayer().GetTeam() == TEAM_SPECTATOR
}

TeamsScoreboardPlayer function GetPlayer( int teamIndex, int row )
{
	TeamsScoreboardPlayer data

	if ( (GetGameState() >= eGameState.Resolution) )
	{
		if( teamsScoreboardCachedData.teamsOfPlayers.len() > teamIndex )
		{
			array<TeamsScoreboardPlayer> cachedTeamData = teamsScoreboardCachedData.teamsOfPlayers[teamIndex]
			if ( cachedTeamData.len() > row )
				data = cachedTeamData[ row ]
		}
	}
	else
	{
		array<entity> teamPlayers = ScoreboardMenu_GetPlayersOnTeam( teamIndex )
		if ( teamPlayers.len() > row )
		{
			entity player = teamPlayers[ row ]

			if ( LoadoutSlot_IsReady( ToEHI( player ), Loadout_Character() ) )
			{
				data = GetTeamsScoreboardPlayer( player )
			}
		}
	}

	return data
}

int function ScoreboardMenu_GetTeamSize( int teamIndex )
{
	if ( (GetGameState() >= eGameState.Resolution) )
	{
		array<TeamsScoreboardPlayer> cachedTeamData = teamsScoreboardCachedData.teamsOfPlayers[teamIndex]
		return cachedTeamData.len()
	}
	else
	{
		array<entity> teamPlayers = ScoreboardMenu_GetPlayersOnTeam( teamIndex )
		return teamPlayers.len()
	}

	return 0
}

array<entity> function ScoreboardMenu_GetPlayersOnTeam( int teamIndex )
{
	array<entity> teamPlayers =  []

	if ( AllianceProximity_IsUsingAlliances() )
	{
		teamPlayers = AllianceProximity_GetAllPlayersInAlliance( teamIndex, false )
	}
	else
	{
		teamPlayers = GetPlayerArrayOfTeam( teamIndex + 2 )
	}

	if( IsObserverInStartOfPrivateGame() )
	{
		teamPlayers.sort( SortPlayersByLockStep )
	}
	else if( IsObserverInPrivateGame() )
	{
		teamPlayers.sort( SortPlayersByName )
	}
	else if( IsValid(file.Callback_SortScoreboardPlayers) )
	{
		ScoreboardData data = GetScoreboardData()
		teamPlayers = file.Callback_SortScoreboardPlayers( teamPlayers, data )
	}

	return teamPlayers
}

int function GetTeamCount()
{
	if ( AllianceProximity_IsUsingAlliances() )
		return AllianceProximity_GetMaxNumAlliances()
	else
		return GetCurrentPlaylistVarInt( "max_teams", 2 )

	unreachable
}

int function Teams_GetMaxTeamSize()
{
	if( AllianceProximity_IsUsingAlliances() )
		return AllianceProximity_GetMaxNumPlayersPerAlliance()
	else
		return GetMaxTeamSizeForPlaylist( GetCurrentPlaylistName() )

	unreachable
}

                  
void function ClientCallback_Teams_SetScoreboardData( var panel )
{
	array<int> teams = GetAllValidPlayerTeams()
	int maxTeamSize = GetMaxTeamSizeForPlaylist( GetCurrentPlaylistName() )

	int localPlayerTeamIndex

	entity localplayer = GetLocalClientPlayer()            
	if ( localplayer.GetTeam() == TEAM_SPECTATOR )
		localPlayerTeamIndex = -1
	else
	{
		if( AllianceProximity_IsUsingAlliances() )
			localPlayerTeamIndex = AllianceProximity_GetAllianceFromTeam( localplayer.GetTeam() )
		else
			localPlayerTeamIndex = localplayer.GetTeam() - 2
	}

	                                                                              
	int mode = GetCurrentPlaylistVarInt( "scoreboard_mode_id", 0 )
	RunUIScript( "UI_SetScoreboardTeamData", panel, GetTeamCount(), Teams_GetMaxTeamSize(), localPlayerTeamIndex, mode )

}

                  
void function ClientCallback_Teams_SetScoreboardTitle( var panel )
{
	string titleText = ""
	if( IsValid(file.Callback_ScoreboardTitle) )
	{
		titleText = file.Callback_ScoreboardTitle()
	}

	RunUIScript( "UI_SetScoreboardTitle", panel, titleText )
}

void function ClientCallback_Teams_CloseScoreboard()
{
	Signal( clGlobal.levelEnt, "Teams_HideScoreboard" )
}

void function ClientCallback_Teams_OnPlayerClicked(  int teamIndex, int row  )
{
	TeamsScoreboardPlayer data = GetPlayer( teamIndex, row )

	if( IsPrivateMatch() && GetLocalClientPlayer().IsObserver() && GetLocalClientPlayer().GetTeam() == TEAM_SPECTATOR && GetGameState() < eGameState.Resolution )
	{
		entity player = data.player
		if ( !IsValid( player ) )
			return

		if ( !IsAlive( player ) )
			return

		if ( GetLocalClientPlayer().GetObserverTarget() == player )
			return

		Remote_ServerCallFunction( "ClientCallback_PrivateMatchChangeObserverTarget", player )
		RunUIScript( "ClosePrivateMatchGameStatusMenu", null )
	}
	else
	{
		if( IsValid( file.Callback_OnPlayerClick ) && data.isValid )
			file.Callback_OnPlayerClick( data.playerEHI )
	}
}

              
void function UICallback_ScoreboardMenu_BindTeamHeader( var header, var frame, int teamIndex, float rowWidth )
{
	thread Thread_Teams_PopulateHeaderRui( header, frame, teamIndex, rowWidth )
}

void function UICallback_ScoreboardMenu_BindTeamRow( var panel, var button, int teamIndex, int row, float rowWidth )
{
	thread Threaded_PopulateRowForPlayer( panel, button, teamIndex, row, rowWidth )
}

void function UICallback_Scoreboard_OnReportClicked( var button, int teamIndex, int row )
{
	TeamsScoreboardPlayer data = GetPlayer( teamIndex, row )

	if( data.isValid )
		ReportPlayer( data.playerEHI )
}

            
void function Teams_OnResolution()
{
	                              
	teamsScoreboardCachedData.teamsOfPlayers.clear()                                       

	int teamCount = GetTeamCount()
	int maxTeamSize = Teams_GetMaxTeamSize()

	for( int i = 0; i < teamCount; i++ )
	{
		array<entity> teamPlayers = ScoreboardMenu_GetPlayersOnTeam( i )
		array< TeamsScoreboardPlayer > players

		for( int j = 0; j < teamPlayers.len(); j++ )
		{
			TeamsScoreboardPlayer playerData = GetTeamsScoreboardPlayer( teamPlayers[j] )
			players.append( playerData )
		}

		teamsScoreboardCachedData.teamsOfPlayers[i] <- players
	}
}

        
void function Thread_Teams_PopulateHeaderRui(var header, var frame, int teamIndex, float rowWidth )
{
	EndSignal( clGlobal.levelEnt, "Teams_HideScoreboard" )
	EndSignal( clGlobal.levelEnt, "Teams_OnResolutionChanged" )

	var headerRui = Hud_GetRui( header )
	var frameRui = Hud_GetRui( frame )

	while ( true )
	{
		ScoreboardData data = GetScoreboardData()
		Teams_PopulateHeaderRui( headerRui, teamIndex, data, rowWidth )
		Teams_PopulateFrameRui( frameRui, teamIndex, data, rowWidth )

		int team = Teams_NormalizeTeamIndex( teamIndex )

		if( IsValid(file.Callback_Header) )
			file.Callback_Header( headerRui, frameRui, team )


		WaitFrame()
	}
}
void function Teams_PopulateHeaderRui( var scoreHeaderRui, int teamIndex, ScoreboardData data, float rowWidth )
{
	RuiSetInt( scoreHeaderRui, "numScoreColumns", data.numScoreColumns )

	for( int i = 0; i < data.numScoreColumns; i++ )
	{
		RuiSetString( scoreHeaderRui, "playerScore" + ( i + 1 ) + "Image", data.columnDisplayIcons[i] )
		RuiSetFloat( scoreHeaderRui, "playerScore" + ( i + 1 ) + "ImageScale", data.columnDisplayIconsScale[i] )
		RuiSetInt( scoreHeaderRui, "playerScore" + ( i + 1 ) + "NumDigits", data.columnNumDigits[i] )
	}

	RuiSetString( scoreHeaderRui, "headerText", Localize( "#TEAM_NUMBERED", (teamIndex + 1) ) )
	RuiSetFloat( scoreHeaderRui, "rowWidth", rowWidth )
	RuiSetInt( scoreHeaderRui, "gameState", GetGameState() )

	vector teamColor
	if( IsValid(file.Callback_GetTeamColor) )
		teamColor = file.Callback_GetTeamColor( Teams_NormalizeTeamIndex( teamIndex ) )
	else
		teamColor = SrgbToLinear( GetSkydiveSmokeColorForTeam( teamIndex + TEAM_IMC) / 255.0 )

	RuiSetColorAlpha( scoreHeaderRui, "teamColor", teamColor, 1 )
}

vector function Teams_GetTeamColor( int teamIndex )
{
	vector teamColor
	if( IsValid(file.Callback_GetTeamColor) )
		teamColor = file.Callback_GetTeamColor( Teams_NormalizeTeamIndex( teamIndex ) )
	else
		teamColor = SrgbToLinear( GetSkydiveSmokeColorForTeam( teamIndex + TEAM_IMC) / 255.0 )

	return teamColor
}

void function Teams_PopulateFrameRui( var scoreFrameRui, int teamIndex, ScoreboardData data, float rowWidth )
{
	vector teamColor
	if( IsValid(file.Callback_GetTeamColor) )
		teamColor = file.Callback_GetTeamColor( Teams_NormalizeTeamIndex( teamIndex ) )
	else
		teamColor = SrgbToLinear( GetSkydiveSmokeColorForTeam( teamIndex + TEAM_IMC ) / 255.0 )

	RuiSetColorAlpha( scoreFrameRui, "teamColor", teamColor, 1 )
}

int function Teams_NormalizeTeamIndex( int teamIndex )
{
	int team = -1

	if( AllianceProximity_IsUsingAlliances() )
		team = teamIndex
	else
		team = teamIndex + 2

	return team
}

        
  
                                                                                                               
  
TeamsScoreboardPlayer function GetTeamsScoreboardPlayer( entity player )
{
	TeamsScoreboardPlayer data
	
	EHI playerEHI = ToEHI( player )

	entity localPlayer = GetLocalViewPlayer()
	int localTeam = localPlayer.GetTeam()

	ItemFlavor character = LoadoutSlot_GetItemFlavor( playerEHI, Loadout_Character() )

	data.playerEHI = playerEHI
	data.player = player
	data.playerName = GetPlayerNameFromEHI( playerEHI )
	data.playerImage = CharacterClass_GetGalleryPortrait( character )

	if( IsObserverInStartOfPrivateGame() && IsValid( player ) )
	{
		int lockStepIndex = GetGlobalNetInt( CHARACTER_SELECT_NETVAR_LOCK_STEP_INDEX )
		int playerLockStepIndex = player.GetPlayerNetInt( CHARACTER_SELECT_NETVAR_LOCK_STEP_PLAYER_INDEX )

		if(  lockStepIndex <= playerLockStepIndex )
			data.playerImage  = $""
	}

	data.playerBG = CharacterClass_GetGalleryPortraitBackground( character )
	data.isRatingLeader = GradeFlagsHas( player, eTargetGrade.EXP_LEADER )
	data.isKillLeader = GradeFlagsHas( player, eTargetGrade.CHAMP_KILLLEADER )
	data.isLocalPlayer = player == localPlayer

	data.respawnStatus = (GetGameState() >= eGameState.Resolution)? 0 : player.GetPlayerNetInt( "respawnStatus" )
	data.isDisconnected = (GetGameState() >= eGameState.Resolution)? false : !player.IsConnectionActive()
	data.bleedoutEndTime = (GetGameState() >= eGameState.Resolution)? 0 : int( player.GetPlayerNetTime( "bleedoutEndTime" ) )

	data.customCharacterColor = SrgbToLinear( GetPlayerInfoColor( player ) / 255.0 )
	data.useCustomCharacterColor = localTeam == player.GetTeam()
	data.enemyBackgroundColor =  SrgbToLinear( GetKeyColor( COLORID_ENEMY ) / 255.0 )

                         
		data.useCustomCharacterColor = data.useCustomCharacterColor || IsGunGameActive()
       

	if( AllianceProximity_IsUsingAlliances() )
	{
		data.team = AllianceProximity_GetAllianceFromTeam( player.GetTeam() )

		if( localTeam == TEAM_SPECTATOR )
		{
			data.isFriendly = false
			data.useCustomCharacterColor = false
		}
		else
		{
			data.isFriendly = AllianceProximity_GetAllianceFromTeam( player.GetTeam() ) == AllianceProximity_GetAllianceFromTeam( localTeam )
			if ( (GetGameState() >= eGameState.Resolution) )
			{
				data.useCustomCharacterColor = AllianceProximity_GetOriginalPlayerTeam_FromPlayerEHI( ToEHI( localPlayer ) ) == AllianceProximity_GetOriginalPlayerTeam_FromPlayerEHI( playerEHI )
			}
		}
	}
	else
	{
		data.team = player.GetTeam()
		data.isFriendly = player.GetTeam() == localTeam
	}

	data.isValid = true

	array< string > scores = GetPlayerScores( player )
	ScoreboardData gameData = GetScoreboardData()

	for( int j = 0; j < gameData.numScoreColumns; j++ )
	{
		TeamsScoreboardPlayerStats stats
		stats.score = scores[j]
		stats.numDigits = gameData.columnNumDigits[j]

		data.stats.append(stats)
	}

	return data
}

void function Threaded_PopulateRowForPlayer( var panel, var button, int teamIndex, int row, float rowWidth )
{
	var rui = Hud_GetRui( button )
	EndSignal( clGlobal.levelEnt, "Teams_HideScoreboard" )
	EndSignal( clGlobal.levelEnt, "Teams_OnResolutionChanged" )

	while( true )
	{
		TeamsScoreboardPlayer data = GetPlayer( teamIndex, row )
		Hud_Show( button )
		if ( data.isValid )
		{
			bool isEnabled = true
			if( IsObserverInStartOfPrivateGame() )
				isEnabled = !IsObserverInStartOfPrivateGame()
			else if( IsValid( file.Callback_IsEnabled ) )
				isEnabled = file.Callback_IsEnabled()

			if( isEnabled )
				RunUIScript( "UI_ToggleReportTooltip", button, !data.isLocalPlayer )
			else
				RunUIScript( "UI_ToggleReportTooltip", button, false )

			Hud_SetEnabled( button, isEnabled )
			Hud_SetLocked( button, false )
			                                         


			RuiSetBool( rui, "isEmpty", false )
			RuiSetString( rui, "status", "" )
			Teams_PopulatePlayerRow( rui, data, rowWidth )

		}
		else
		{
			int teamSize = ScoreboardMenu_GetTeamSize( teamIndex )
			bool modeHasJIP = GamemodeUtility_IsJIPEnabled() && !IsPrivateMatch()
			RunUIScript( "UI_ToggleReportTooltip", button, false )
			Hud_SetLocked( button, true )
			Hud_SetEnabled( button, false )
			if( row == teamSize && modeHasJIP )
			{
				RuiSetString( rui, "status", Localize("#JIP_SEARCHING_FOR_PLAYER" ) )
				RuiSetBool( rui, "isJIP", true )
				RuiSetBool( rui, "isEmpty", false )
				RuiSetImage( rui, "playerImage", $"rui/menu/buttons/lobby_character_select/portrait_selecting_tint" )

				vector teamColor
				if( IsValid(file.Callback_GetTeamColor) )
					teamColor = file.Callback_GetTeamColor( Teams_NormalizeTeamIndex( teamIndex ) )
				else
					teamColor = SrgbToLinear( GetSkydiveSmokeColorForTeam( teamIndex + TEAM_IMC ) / 255.0 )

				RuiSetColorAlpha( rui, "playerImageTint", teamColor, 1.0 )
			}
			else
			{
				RuiSetString( rui, "status", "" )
				RuiSetBool( rui, "isEmpty", true )
				RuiSetBool( rui, "isJIP", false )
				RuiSetImage( rui, "playerImage", $"" )
				RuiSetColorAlpha( rui, "playerImageTint", < 1.0, 1.0, 1.0 >, 1.0 )
			}

			RuiSetInt( rui, "respawnStatus", 0 )
			RuiSetString( rui, "playerName", "" )
			RuiSetBool( rui, "isFriendly", true )
			RuiSetBool( rui, "isRatingLeader", false )
			RuiSetBool( rui, "isKillLeader", false )
			RuiSetBool( rui, "isLocalPlayer", false )
			RuiSetBool( rui, "useCustomCharacterColor", false )
			RuiSetString( rui, "platformIcon", $"" )
			RuiSetInt( rui, "numScoreColumns", 0 )
		}


		wait 0.2
	}
}

void function Teams_PopulatePlayerRow( var rui, TeamsScoreboardPlayer data, float rowWidth )
{
	RuiSetFloat( rui, "rowWidth", rowWidth )
	RuiSetInt( rui, "screenHeight", GetScreenSize().height )
	RuiSetInt( rui, "screenWidth", GetScreenSize().width )
	RuiSetInt( rui, "numScoreColumns", data.stats.len() )

	for( int j = 0; j < data.stats.len(); j++ )
	{
		RuiSetString( rui, "playerScore" + ( j + 1 ) , data.stats[j].score )
		RuiSetInt( rui, "playerScore" + ( j + 1 ) + "NumDigits" , data.stats[j].numDigits )
	}

	if ( CrossplayEnabled() && CrossplayUserOptIn() )
		RuiSetString( rui, "platformIcon", data.platformIcon )

	RuiSetString( rui, "playerName", data.playerName )
	RuiSetBool( rui, "isFriendly", data.isFriendly )
	RuiSetImage( rui, "playerImage", data.playerImage )
	RuiSetImage( rui, "playerBG", data.playerBG )
	RuiSetBool( rui, "isRatingLeader", data.isRatingLeader )
	RuiSetBool( rui, "isKillLeader", data.isKillLeader )
	RuiSetBool( rui, "isLocalPlayer", data.isLocalPlayer )
	RuiSetInt( rui, "respawnStatus", data.respawnStatus )
	RuiSetInt( rui, "bleedoutEndTime", data.bleedoutEndTime )
	RuiSetColorAlpha( rui, "customCharacterColor", data.customCharacterColor, 1.0 )
	RuiSetBool( rui, "useCustomCharacterColor", data.useCustomCharacterColor )
	RuiSetBool( rui, "isJIP", false )
	RuiSetColorAlpha( rui, "playerImageTint", < 1.0, 1.0, 1.0 >, 1.0 )
	RuiSetColorAlpha( rui, "enemyBackgroundColor", data.enemyBackgroundColor, 1.0 )
}

int function SortPlayersByLockStep( entity a, entity b )
{
	int aStepIndex = a.GetPlayerNetInt( CHARACTER_SELECT_NETVAR_LOCK_STEP_PLAYER_INDEX )
	int bStepIndex = b.GetPlayerNetInt( CHARACTER_SELECT_NETVAR_LOCK_STEP_PLAYER_INDEX )

	if ( aStepIndex > bStepIndex )
		return 1

	if ( aStepIndex < bStepIndex )
		return -1

	return 0
}

int function SortPlayersByName( entity a, entity b )
{
	if ( a.GetPlayerName() > b.GetPlayerName() )
		return 1

	if ( a.GetPlayerName() < b.GetPlayerName() )
		return -1

	return 0
}